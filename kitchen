#!/usr/bin/env zsh
# Build and run Python development containers.

CONTEXT="$PWD"                    # Path to build context
NAMETAG="$(basename "$CONTEXT")"  # Name of image to build
PKGFILE="requirements.txt"        # List of packages to install
WORKDIR="/context"                # Working directory inside container

help() {
  echo "Run disposable containers built from $CONTEXT"
  echo
  echo "COMMANDS"
  echo "alamode         Don't make me think, just do all the things"
  echo "bake            Build $NAMETAG image from $CONTEXT"
  echo "clean           Delete $NAMETAG containers and leftovers"
  echo "debug [SCRIPT]  Run SCRIPT with interactive debugger"
  echo "eighty_six      Delete $NAMETAG image, containers, and leftovers"
  echo "freeze          Update $PKGFILE and rebuild image"
  echo "help            Show commands, examples, and Docker objects"
  echo "runit [CMD]     Run CMD in an interactive $NAMETAG container"
  echo "serve [CMD]     Same as runit, but with $WORKDIR mounted from $CONTEXT"
  echo
  echo "EXAMPLES"
  echo "cd path/to/my/project"
  echo "./kitchen alamode"
  echo "./kitchen runit ls -la"
  echo "./kitchen serve soda --fizz=4 0 10"
  echo "./kitchen debug src/pydiner/utensils.py"
  echo
  echo "DOCKER OBJECTS"
  echo "---- Images ----" && docker image ls | sort
  echo "---- Volumes ----" && docker volume ls | sort
  echo "---- Networks ----" && docker network ls
  echo "---- Containers ----" && docker ps --all
}
alamode() {
  bake && freeze && serve "$@"
}
bake() {
  touch "$PKGFILE" &&
  docker build --tag "$NAMETAG" --build-arg WORKDIR="$WORKDIR" "$CONTEXT" &&
  docker image prune --force
}
clean() {
  docker ps --quiet --filter ancestor="$NAMETAG" | xargs docker rm --force &&
  docker system prune "$@"
}
debug() {
  serve python -m pdb "$@"
}
eighty_six() {
  docker rmi --force "$NAMETAG" &&
  clean "$@"
}
freeze() {
  docker run --rm "$NAMETAG" pip freeze --exclude-editable > "$PKGFILE" &&
  bake
}
runit() {
  docker run --rm --interactive --tty "$NAMETAG" "$@"
}
serve() {
  docker run --rm --interactive --tty \
    --volume "${CONTEXT}:${WORKDIR}:delegated" \
    --hostname "${NAMETAG}" \
    "$NAMETAG" "$@"
}
if [ $# -eq 0 ]; then help; else "$@"; fi
